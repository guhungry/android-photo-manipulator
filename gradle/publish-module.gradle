apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

// Dokka 2.x configuration
tasks.named('dokkaHtml') {
    dokkaSourceSets.configureEach {
        // Configure Dokka base plugin for separate inherited members
        pluginsConfiguration.html {
            separateInheritedMembers = true
        }
    }
}

// Configure Dokka to generate Javadoc format
tasks.register('dokkaJavadocTask') {
    dependsOn 'dokkaGeneratePublicationHtml'
    doLast {
        // Create javadoc directory if it doesn't exist
        def javadocDir = layout.buildDirectory.dir('dokka/javadoc').get().asFile
        javadocDir.mkdirs()

        // Copy HTML docs to javadoc directory
        def htmlDir = layout.buildDirectory.dir('dokka/html').get().asFile
        if (htmlDir.exists()) {
            copy {
                from htmlDir
                into javadocDir
            }
        }
    }
}

tasks.register('javadocJar', Jar) {
    dependsOn 'dokkaJavadocTask'
    archiveClassifier.set('javadoc')
    from layout.buildDirectory.dir('dokka/javadoc')
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = publishedGroupId
version = libraryVersion

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                tasks.named("generateMetadataFileForReleasePublication").configure { dependsOn("androidSourcesJar") }
                groupId = publishedGroupId
                artifactId = artifact
                version = libraryVersion
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("${layout.buildDirectory}/libs/${project.getName()}-${version}.jar")
                }

                pom {
                    packaging = 'aar'
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}