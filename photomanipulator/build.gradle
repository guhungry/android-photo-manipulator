import com.android.Version

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

static def supportsNamespace() {
    def parsed = Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
    def major = parsed[0].toInteger()
    def minor = parsed[1].toInteger()

    // Namespace support was added in 7.3.0
    return (major == 7 && minor >= 3) || major >= 8
}

android {
    if (supportsNamespace()) {
        namespace 'com.guhungry.photomanipulator'

        sourceSets {
            main {
                manifest.srcFile "src/main/AndroidManifestNew.xml"
            }
        }
    }
    compileSdk 34

    defaultConfig {
        minSdk 16
        versionCode 2
        versionName "1.3.0"
        targetSdk 34
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        aarMetadata {
            minCompileSdk = 16
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            defaultConfig.minSdk 21
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        languageVersion = "1.5"
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
    testFixtures {
        enable = true
    }
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "androidx.exifinterface:exifinterface:1.3.7"

    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation 'org.hamcrest:hamcrest-library:3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // Mockito
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
}
repositories {
    mavenCentral()
}

ext {
    publishedName = 'com.guhungry.android:photo-manipulator'

    publishedGroupId = 'com.guhungry.android'
    libraryName = 'AndroidPhotoManipulator'
    artifact = 'photo-manipulator'

    libraryDescription = 'Android Image Processing API to edit photo programmatically'

    siteUrl = 'https://github.com/guhungry/android-photo-manipulator'
    gitUrl = 'https://github.com/guhungry/android-photo-manipulator.git'

    libraryVersion = android.defaultConfig.versionName

    developerId = 'guhungry'
    developerName = 'Woraphot Chokratanasombat'
    developerEmail = 'guhungry@gmail.com'

    licenseName = 'MIT'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/publish-module.gradle"
apply plugin: 'org.sonarqube'
